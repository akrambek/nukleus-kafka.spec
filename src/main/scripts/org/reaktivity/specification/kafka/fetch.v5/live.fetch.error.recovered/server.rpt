#
# Copyright 2016-2018 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newNetworkRouteRef ${nuklei:newReferenceId()} # external scope

property newTimestamp ${kafka:timestamp()}

property networkAccept "nukleus://kafka/streams/source"
property networkAcceptWindow 8192

property errorCode 3s       # topic or partition not found

accept ${networkAccept}
  option nukleus:route  ${newNetworkRouteRef}
  option nukleus:window ${networkAcceptWindow}
  option nukleus:transmission "duplex"
  option nukleus:byteorder "network"

# Metadata connection
accepted
connected

# Metadata request topic test1
read 22         # Size int32
read 3s         # ApiKey int16 (Metadata)
read 5s         # ApiVersion int16
read (int:metadataRequestId)
read -1s        # ClientId string (null)
read 1          # [TopicName] array length
  read 5s "test1"
read [0x00]     # allow_auto_topic_creation (boolean)

write 86        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # brokers array length
  write 1       # broker id
  write 7s "broker1"
  write 9093    # port int32
  write -1s     # rack string (null)
write 9s "cluster 1"
write 1         # controller broker id
write 1         # topic array length
  write 0s      # error code
  write 5s "test1"
  write byte 0  # is_internal
  write 1       # partition array length
    write 0s    # error code
    write 0     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

read 63         # Size int32
read 32s        # ApiKey int16 (DescribeConfigs)
read 0s         # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [Resources] array length
read [0x02]     # resource type int8 (topic)
read 5s "test1" # topic name
read 2          # config_names count
read 14s "cleanup.policy"
read 19s "delete.retention.ms"

write 89        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # resources count
write 0s        # error code
write -1s       # error message
write [0x02]    # resource type
write 5s "test1"            # topic name
write 2         # config entries count
write 14s "cleanup.policy"  # config name
write 6s "delete"           # config  value
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean
write 19s "delete.retention.ms"
write 8s "86400000"
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean

# Metadata request topic test2
read 22         # Size int32
read 3s         # ApiKey int16 (Metadata)
read 5s         # ApiVersion int16
read (int:metadataRequestId)
read -1s        # ClientId string (null)
read 1          # [TopicName] array length
  read 5s "test2"
read [0x00]     # allow_auto_topic_creation (boolean)

write 86        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # brokers array length
  write 1       # broker id
  write 7s "broker1"
  write 9093    # port int32
  write -1s     # rack string (null)
write 9s "cluster 1"
write 1         # controller broker id
write 1         # topic array length
  write 0s      # error code
  write 5s "test2"
  write byte 0  # is_internal
  write 1       # partition array length
    write 0s    # error code
    write 0     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

read 63         # Size int32
read 32s        # ApiKey int16 (DescribeConfigs)
read 0s         # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [Resources] array length
read [0x02]     # resource type int8 (topic)
read 5s "test2" # topic name
read 2          # config_names count
read 14s "cleanup.policy"
read 19s "delete.retention.ms"

write 89        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # resources count
write 0s        # error code
write -1s       # error message
write [0x02]    # resource type
write 5s "test2"            # topic name
write 2         # config entries count
write 14s "cleanup.policy"  # config name
write 6s "delete"           # config  value
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean
write 19s "delete.retention.ms"
write 8s "86400000"
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean

# Metadata is refreshed for topic test1 after the fetch error 
# code is processed
read await FETCH_RESPONSE_WITH_ERROR_WRITTEN

read 22         # Size int32
read 3s         # ApiKey int16 (Metadata)
read 5s         # ApiVersion int16
read (int:metadataRequestId)
read -1s        # ClientId string (null)
read 1          # [TopicName] array length
  read 5s "test1"
read [0x00]     # allow_auto_topic_creation (boolean)

write 86        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # brokers array length
  write 1       # broker id
  write 7s "broker1"
  write 9093    # port int32
  write -1s     # rack string (null)
write 9s "cluster 1"
write 1         # controller broker id
write 1         # topic array length
  write 0s      # error code
  write 5s "test1"
  write byte 0  # is_internal
  write 1       # partition array length
    write 0s    # error code
    write 0     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

read 63         # Size int32
read 32s        # ApiKey int16 (DescribeConfigs)
read 0s         # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [Resources] array length
read [0x02]     # resource type int8 (topic)
read 5s "test1" # topic name
read 2          # config_names count
read 14s "cleanup.policy"
read 19s "delete.retention.ms"

write 89        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # resources count
write 0s        # error code
write -1s       # error message
write [0x02]    # resource type
write 5s "test1"            # topic name
write 2         # config entries count
write 14s "cleanup.policy"  # config name
write 6s "delete"           # config  value
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean
write 19s "delete.retention.ms"
write 8s "86400000"
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean

write notify METADATA_REFRESH_RESPONSE_WRITTEN

# Fetch stream
accepted
read nukleus:begin.ext ${tcp:beginExtRemoteHost("broker1", 9093)}
connected

# Fetch request for topic test1
read 66
read 1s
read 5s
read (int:requestId)
read -1s
read -1
read [0..4]
read 1
read [0..4]
read [0x00]
read 1
read 5s "test1"
read 1
read 0
read 0L
read -1L
read [0..4]

read notify FIRST_FETCH_REQUEST_RECEIVED

write await WRITE_FIRST_FETCH_RESPONSE

# No messages for test1
write 61
write ${requestId}
write 0
write 1
write 5s "test1"
write 1         # partition count
write 0         # partition id
write 0s
write 0L        # high water mark
write -1L       # last stable offset
write 0L        # log start offset
write -1        # aborted transaction count
write 0         # record set size

# Fetch request for topics test1 and test2 gives error code for test1
read 101
read 1s
read 5s
read (int:requestId1)
read -1s
read -1
read [0..4]
read 1
read [0..4]
read [0x00]
read 2

read 5s "test1"
read 1
read 0          # Partition
read 0L         # offset
read -1L
read [0..4]

read 5s "test2"
read 1
read 0          # Partition
read 0L         # offset
read -1L
read [0..4]

write 110
write ${requestId1}
write 0
write 2         # Number of topic responses

write 5s "test1"
write 1         # Number of partition responses
write 0         # Partition
write ${errorCode}          # partition error code
write 0L        # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 0         # length of record set

write 5s "test2"
write 1         # Number of partition responses
write 0         # Partition
write 0s        # partition error code
write 0L       # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 0         # length of record set

write notify FETCH_RESPONSE_WITH_ERROR_WRITTEN

# Fetch request for topic test2 only
read 66
read 1s
read 5s
read (int:requestId)
read -1s
read -1
read [0..4]
read 1
read [0..4]
read [0x00]
read 1
read 5s "test2"
read 1
read 0
read 0L
read -1L
read [0..4]

write await METADATA_REFRESH_RESPONSE_WRITTEN

# No messages for test2
write 61
write ${requestId}
write 0
write 1
write 5s "test2"
write 1         # partition count
write 0         # partition id
write 0s
write 0L        # high water mark
write -1L       # last stable offset
write 0L        # log start offset
write -1        # aborted transaction count
write 0         # record set size

# Fetch request for topics test1 and test2 gives a message for test1
read 101
read 1s
read 5s
read (int:requestId1)
read -1s
read -1
read [0..4]
read 1
read [0..4]
read [0x00]
read 2

read 5s "test1"
read 1
read 0          # Partition
read 0L         # offset
read -1L
read [0..4]

read 5s "test2"
read 1
read 0          # Partition
read 0L         # offset
read -1L
read [0..4]

write 190
write ${requestId1}
write 0
write 2         # Number of topic responses

write 5s "test1"
write 1         # Number of partition responses
write 0         # Partition
write 0s        # partition error code
write 1L        # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 80        # length of record set
write 0L        # first offset
write 68        # length of record batch
write 0
write [0x02]
write 0x4e8723aa
write 0s
write 0         # last offset delta
write ${newTimestamp}       # first timestamp
write ${newTimestamp}       # maximum timestamp
write -1L
write -1s
write -1        # first sequence
write 1         # number of records
write ${kafka:varint(18)}
write [0x00]    # attributes
write ${kafka:varint(0)}    # timestamp delta
write ${kafka:varint(0)}    # offset delta
write ${kafka:varint(-1)}   # key length
write ${kafka:varint(12)}   # value length
write "Hello, world"
write ${kafka:varint(0)}

write 5s "test2"
write 1         # Number of partition responses
write 0         # Partition
write 0s        # partition error code
write 0L        # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 0         # length of record set


